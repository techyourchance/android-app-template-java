apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.techyourchance.template"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            // TODO: CHANGE TO TRUE AFTER PROVIDING ../release.properties FILE FOR RELEASE BUILD
            if (false) {
                def projectPropsFile = file('../release.properties')

                if (projectPropsFile.exists()) {
                    Properties props = new Properties()
                    props.load(new FileInputStream(projectPropsFile))

                    storeFile file(props['KEYSTORE_FILE'])
                    storePassword props['STORE_PASSWORD']
                    keyAlias props['KEY_ALIAS']
                    keyPassword props['KEY_PASSWORD']
                } else {
                    throw new FileNotFoundException("release signing properties file doesn't exist")
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

ext {
    androidxAppcompatVersion = '1.2.0'
    androidxAnnotationVersion = '1.1.0'
    fragmentVersion = "1.2.5"
    daggerVersion = "2.30.1"
    retrofitVersion = "2.6.1"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.annotation:annotation:${androidxAnnotationVersion}"
    implementation "androidx.appcompat:appcompat:${androidxAppcompatVersion}"
    implementation "androidx.fragment:fragment:$fragmentVersion"

    // fragment navigation
    implementation 'com.ncapdevi:frag-nav:3.3.0'

    // dialogs
    implementation 'com.techyourchance:dialoghelper:0.9.0'
    
    // multithreading
    implementation "com.techyourchance.threadposter:threadposter:0.8.3"

    // event bus
    implementation 'org.greenrobot:eventbus:3.1.1'

    // logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // dependency injection
    implementation "com.google.dagger:dagger:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    // networking
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"

    // unit testing
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:2.18.3'
}